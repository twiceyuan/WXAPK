apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk = 34

    buildFeatures {
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.twiceyuan.wxapk"
        minSdk = 16
        targetSdk = 34
        namespace = "com.twiceyuan.wxapk"
        versionCode getAppVersionCode()
        versionName getLastTagName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (System.getenv("CI") != null) {
                storeFile file(System.env.TWICEYUAN_KEYSTORE)
                storePassword System.env.TWICEYUAN_KEYSTORE_PASSWD
                keyAlias System.env.TWICEYUAN_KEY_ALIAS
                keyPassword System.env.TWICEYUAN_KEY_PASSWD
            } else {
                String keystore = System.env.WXAPK_KEYSTORE
                storeFile file(keystore == null ? "/dev/null" : keystore)
                storePassword System.env.WXAPK_KEYSTORE_PASSWD
                keyAlias System.env.WXAPK_KEY_ALIAS
                keyPassword System.env.WXAPK_KEY_PASSWD
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    applicationVariants.configureEach { variant ->
        if (variant.buildType.name != "release") return
        outputs.forEach {
            def buildTypeName = variant.buildType.name
            def version = "$versionName($versionCode)"
            it.outputFileName = "WXAPK-${version}-${buildTypeName}.apk"
        }
    }

    sourceSets.configureEach {
        languageSettings {
            languageVersion = "2.0"
        }
    }
}

// 根据应用提交记录数来生成版本号
def getAppVersionCode() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'tag', '--list'
        standardOutput = stdout
    }
    // 后面 + 的数字为临时发版没有打 tag 的版本。尽量避免这种情况
    return stdout.toString().split("\n").size() + 1
}

// 获取最新的 tag 名称
def getLastTagName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--abbrev=0', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

tasks.register('printVersionInfo') {
    doLast {
        println "VersionName: ${getLastTagName()}, VersionCode: ${getAppVersionCode()}"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
}
